apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'


configurations {
    ktlint
}

android {
    compileSdkVersion 34
    defaultConfig {
        applicationId "com.indiewalk.watchdog.earthquake"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 10
        versionName "2.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        production {
            dimension "EarthquakeWatchdog"
            resValue "string", "app_name", "EarthquakeWatchdog"
        }

        myTesting {
            dimension "EarthquakeWatchdog"
            resValue "string", "app_name", "EarthquakeWatchdog Test"
            applicationIdSuffix ".testing"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled true
    }

    viewBinding {
        enabled true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    namespace 'com.indiewalk.watchdog.earthquake'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$appcompat_vers"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_vers"
    implementation "androidx.legacy:legacy-support-core-utils:$legacy_support_vers"

    // recyclerview
    implementation "androidx.recyclerview:recyclerview:$recyclerview_vers"

    // floating button
    implementation "com.google.android.material:material:$material_vers"

    // Google play services
    implementation "com.google.android.gms:play-services-maps:$gps_maps_vers"
    implementation "com.google.android.gms:play-services-location:$gps_location_vers"

    // Room
    implementation "androidx.room:room-runtime:$room_vers"
    kapt "androidx.room:room-compiler:$room_vers"
    implementation "androidx.room:room-ktx:$room_vers"
    // test helpers
    testImplementation "androidx.room:room-testing:$room_vers"

    // LiveData dependencies
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_vers"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_vers"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext_vers"

    // firebase
    implementation "com.firebase:firebase-jobdispatcher:$jobdispatcher_vers"

    //  Ads through Firebase
    // implementation 'com.google.firebase:firebase-ads:17.1.1'

    // google gms
    implementation "com.google.android.gms:play-services-ads:$gps_ads_vers"

    // GDPR
    implementation "com.google.android.ads.consent:consent-library:$consent_library_vers"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // https://ktlint.github.io/#getting-started
    ktlint 'com.pinterest:ktlint:0.39.0'

    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    implementation "androidx.test:core:1.5.0"

    //  AndroidX Test - local unit tests - JVM testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'androidx.test:runner:1.5.2'
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "androidx.test.ext:junit:1.1.5"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    testImplementation "io.mockk:mockk:1.9.3"



    // AndroidX Test - Instrumented testing
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'androidx.test:rules:1.5.0'



    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }



}


task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
